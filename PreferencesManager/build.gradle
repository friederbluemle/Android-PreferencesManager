apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('version.properties')))

    defaultConfig {
        applicationId "fr.simon.marquis.preferencesmanager"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode versionProps['code'].toInteger()
        versionName versionProps['name']
    }
    signingConfigs {
        release {
            Properties keyProps = new Properties()
            File localPropsFile = file('../local.properties')
            if (localPropsFile.exists()) {
                Properties localProps = new Properties()
                localProps.load(new FileInputStream(localPropsFile))
                assert localProps['keystore.props.file'];
                keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
                storeFile file(keyProps["store"])
            }
            keyAlias = keyProps.hasProperty('alias') ? keyProps["alias"] : ''
            storePassword = keyProps.hasProperty('storePass') ? keyProps["storePass"] : ''
            keyPassword = keyProps.hasProperty('pass') ? keyProps["pass"] : ''
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile files('libs/RootTools.jar')
}
